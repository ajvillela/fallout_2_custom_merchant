/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Klamath Citizen
        Location: Klamath
        Description: Generic citizen in Klamath.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: January 13, 1998

           Updated: April 27, 1998  PLEASE DO NOT CHANGE WITHOUT CHECKING WITH ME -NBK
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_KCITIZEN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_KLAMATH)

#include "..\headers\command.h"
#include "..\headers\kladwtn.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure push_p_proc;

/* Script Specific Procedure Calls */
procedure Node997;                                      // this Node handles the basic START CONDITIONS
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending
procedure Node901;                                      // Random dialogue 1
procedure Node902;                                      // Random dialogue 2
procedure Node903;                                      // Random dialogue 3

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Hostile                    (4)
#define LVAR_Personal_Enemy             (5)

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable pick;
variable In_Timed_Event;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   //CHANGE THESE -nbk
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_KLAMATH);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_CITIZEN);
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
       animate_move_to_tile(tile_num_in_direction(tile_num(self_obj),random(0,5),random(3,7)));
       In_Timed_Event:=0;
   end
end
/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin
   if (((local_var(LVAR_Hostile) == 2)or(local_var(LVAR_Personal_Enemy) == 1)) and (obj_can_see_obj(self_obj,dude_obj))) then begin
      set_local_var(LVAR_Hostile,1);
      Flee_From_Dude
   end
   else if ((random(0,100) == 1) and (In_Timed_Event == 0)) then begin
       In_Timed_Event:=1;
       add_timer_event(self_obj,game_ticks(random(17,37)),1);
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      //CHANGE -nbk
      set_global_var(GVAR_ENEMY_KLAMATH,1);
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   //GetReaction;


   //CURRENTLY KLAMATH CHILDREN JUST HAVE RANDOM DIALOGUE
   //IF THIS CHANGES IN FUTURE THEN JUST UNCOMMENT GETREACTION AND DIALOGUE -nbk
   if (TOWN_REPUTATION < 6 and not(has_trait(TRAIT_PERK, dude_obj, PERK_cult_of_personality))) then call Node901; // edit by killap added not(has_trait(TRAIT_PERK, dude_obj, PERK_cult_of_personality))
   else if (TOWN_REPUTATION > 14 or (has_trait(TRAIT_PERK, dude_obj, PERK_cult_of_personality))) then call Node903; // edit by killap added (has_trait(TRAIT_PERK, dude_obj, PERK_cult_of_personality))
   else call Node902;
   /*
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node000;
      gSay_End;
      end_dialogue;
   end
   */
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin
/* Increment the aligned critter counter*/
   inc_good_critter
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (get_critter_stat(self_obj,STAT_gender) == GENDER_MALE) then
       display_msg(mstr(100));
   else
       display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   if (get_critter_stat(self_obj,STAT_gender) == GENDER_MALE) then
       display_msg(mstr(100));
   else
       display_msg(mstr(101));
end

procedure push_p_proc begin
//player can push this critter
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
   //if (action_being_used == SKILL_STEAL) then begin
      //if (is_success(roll_vs_skill(dude_obj,action_being_used,0))) then begin
      //end
   //end
end

procedure Node901 begin
   floater(random(150,161));
end
procedure Node902 begin
   floater(random(170,188));
end
procedure Node903 begin
   floater(random(190,204));
end
procedure Node997 begin
/*
   if (get_critter_stat(dude_obj,STAT_iq) <= (0-LOW_IQ)) then begin
      call Node007;
   end else if (local_var(LVAR_Herebefore) == 0) then begin
      call Node001;
   end else begin
      call Node001;
   end
*/
end
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end
/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end