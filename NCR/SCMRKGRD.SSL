/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Merk Guard
        Location: NCR Entrance/Council
        Description: Follows merk around

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#include "..\headers\define.h"
//#include "..\headers\<TownName.h>"

#define NAME                    SCRIPT_SCMRKGRD
#define TOWN_REP_VAR         GVAR_TOWN_REP_NCR

#include "..\headers\command.h"
#include "..\headers\ModReact.h"

#include "..\headers\ncr.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;

procedure timerEvent;
procedure checktime;
procedure checkPos;
procedure getTileNum;
procedure finishWalk;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool
#define TIMER_EVENT 1

/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Visible                    (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */
import variable merk_obj;
import variable merk_guard_count;
import variable merk_visible;

/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable self_guard_num := 0;
variable inTransit := 0;

procedure start begin
end

procedure timed_event_p_proc begin
   if (fixed_param == TIMER_EVENT) then
      call timerEvent;
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   merk_guard_count += 1;
   Only_Once:=0;
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_NCR_MERK);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_AI_PACKET,AI_TOUGH_GUARD);

   if (is_loading_game == FALSE) then begin
      if (merk_status == MERK_STATUS_DEAD) then begin
         move_to(self_obj, 7000, 0);
         destroy_object(self_obj);
      end else begin
         self_guard_num := merk_guard_count;
         set_obj_visibility(self_obj, OBJ_INVIS);
         call timerEvent;
      end
   end
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) != 0 or global_var(GVAR_NCR_SCMERK_HOSTILE) != 0) and (obj_can_see_obj(self_obj,dude_obj))) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (obj_in_party(source_obj)) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=1;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION0;

   CheckKarma;

   GetReaction;

   if (inTransit == 1) then
      call Node001;
   else
      call Node002;
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_evil_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   float_msg(self_obj, mstr(103 + Random(0, 3)), FLOAT_MSG_NORMAL);
end

procedure Node002 begin
   float_msg(self_obj, mstr(107 + Random(0, 2)), FLOAT_MSG_NORMAL);
end

procedure timerEvent begin
   variable tile;

   if (not combat_is_initialized) then begin
      if (cur_map_index == checktime) then begin
         if (local_var(LVAR_Visible)) then begin
            call checkPos;
         end else begin
            debug_msg("SCMRKGRD: Should be visible");
            if (merk_visible) then begin
               tile := getTileNum;
               move_to(self_obj, tile, 0);
               set_obj_visibility(self_obj, OBJ_VIS);
               set_local_var(LVAR_Visible, 1);
            end
         end
      end else begin
         if (local_var(LVAR_Visible) and merk_visible == 0) then begin
            if (anim_busy(self_obj) == 1) then begin
               reg_anim_clear(self_obj);
            end
            set_obj_visibility(self_obj, OBJ_INVIS);
            set_local_var(LVAR_Visible, 0);
         end else
            call checkPos;
      end
   end
   add_timer_event(self_obj, ONE_GAME_SECOND, TIMER_EVENT);
end

procedure checkPos begin
   variable tile;

   tile := getTileNum;

   if (anim_busy(self_obj) == 0) then begin
      if (self_tile != tile) then begin
         animate_move_to_tile(tile);
         inTransit := 1;
      end else begin
         inTransit := 0;
         call finishWalk;
      end
   end
end

procedure finishWalk begin
   if (self_guard_num == 1) then begin
      animate_rotation(1);
   end else if (self_guard_num == 2) then begin
      animate_rotation(3);
   end
end

procedure getTileNum begin
   if (self_guard_num == 1) then begin
      return tile_num_in_direction(tile_num(merk_obj), 1, 2);
   end else if (self_guard_num == 2) then begin
      return tile_num_in_direction(tile_num(merk_obj), 4, 1);
   end
end

procedure checktime begin
   variable time := 0;
   time := game_time_hour;

   if (time > 800 and time < 1201) then
      return MAP_NCR_COUNCIL;
   if (time > 1200 and time < 1601) then
      return MAP_NCR_COUNCIL;
   if (time > 400 and time < 2401) then
      return MAP_NCR_BAZAAR;
   if (time > 2400 and time < 801) then
      return MAP_NCR_BAZAAR;
end

//xxxxxxxxxxxxxxxxxxxx